import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

buildscript {
	repositories {
		mavenCentral()
	}
}

plugins {
	id 'java'
	id 'idea'
	id 'org.springframework.boot' version '2.4.3'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'com.google.cloud.tools.jib' version '2.8.0'
	id 'org.openapi.generator' version '5.1.0'
	id 'com.diffplug.spotless' version '5.11.0'
}

group = 'bio.terra.cda'
version = '0.0.9-SNAPSHOT'
sourceCompatibility = '1.11'

repositories {
	mavenCentral()
}

dependencies {
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jdbc'
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
	implementation group: 'javax.validation', name: 'validation-api'
	implementation group: 'org.webjars', name: 'swagger-ui', version: '3.24.0'

	implementation group: 'com.google.cloud', name: 'google-cloud-bigquery', version: '1.124.7'

	// These dependencies are required to load the logback config file.
	implementation group: 'org.codehaus.groovy', name: 'groovy', version: '3.0.7'
	implementation group: 'org.springframework.cloud', name: 'spring-cloud-gcp-starter-logging', version: '1.2.5.RELEASE'

	// -- OpenAPI CodeGen dependencies --
	implementation group: 'io.swagger.core.v3', name: 'swagger-annotations', version: '2.1.7'
	implementation group: 'io.springfox', name: 'springfox-swagger2', version: '3.0.0'
	// -- --

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

// Exclude the Spring logger, so everything will use SLF4J
configurations.all {
	exclude group: "commons-logging", module: "commons-logging"
}

// OpenAPI Server Generation
// - specify the source and target
// - define the generation
// - define the validation
// - define dependencies to include the generated code

def openapiSourceFile = "${projectDir}/src/main/resources/api/service_openapi.yaml"
def openapiTargetDir = "${buildDir}/generated"
def openapiClientTargetDir = "${buildDir}/generated-client"

openApiGenerate {
	generatorName = "spring"
	inputSpec = "${openapiSourceFile}".toString()
	outputDir = "${openapiTargetDir}".toString()
	packageName = "${group}"
	apiPackage = "${group}.generated.controller"
	modelPackage = "${group}.generated.model"
	configOptions = [
	        interfaceOnly : "true",
			useTags : "true",
			library : "spring-boot",
			dateLibrary : "java8",
			generateApiTests : "false",
			generateApiDocumentation : "false",
			generateModelTests : "false",
			generateModelDocumentation : "false",
			"openApiNullable": "false",
	]
}

task buildPythonSdk(type: GenerateTask){
	generatorName = "python"
	packageName = "cda_client"
	inputSpec = "${openapiSourceFile}".toString()
	outputDir = "${openapiClientTargetDir}".toString()
}

openApiValidate {
	inputSpec = "${openapiSourceFile}".toString()
}

task patchGeneratedJava(type: Exec) {
	commandLine "patch", "-p0", "-i", "misc/QueryResponseData.java.patch"
} dependsOn(tasks.openApiGenerate)

compileJava.dependsOn tasks.patchGeneratedJava
sourceSets.main.java.srcDir "${openapiTargetDir}/src/main/java"
ideaModule.dependsOn tasks.patchGeneratedJava

// end of OpenAPI Server Generation

test {
	useJUnitPlatform()
}

spotless {
	java {
		googleJavaFormat()
	}
}
